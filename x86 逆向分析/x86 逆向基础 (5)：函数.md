# 1 函数概述

## 函数定义

在计算机中，程序都是由不同功能的函数组成，而函数是一个程序块，是功能的具体实现，这样就不需要在需要相同功能的地方编写同样的代码，只需要调用函数即可，极大方便了编码，代码结构也一目了然。

一个函数包括函数名、参数、函数体、返回值等部分，一个典型的函数如下：

```c
return_type function_name(arg1, arg2, ..., argN) {
   statement;

   return value;
}
```

函数可以没有返回值，也可以没有参数，也可以有多个参数，还可以是可变参数。

## 函数调用过程

程序通过调用函数来执行对应的功能，函数执行完毕后，又返回调用它的程序继续执行。前者被称为**调用函数**（caller），后者被称为**被调函数**（callee）。

考虑如下 C 代码：

```c
int main(void) {
    ...
    func();

    return 0;
}
```

main 函数为主函数，程序会从 main 函数开始执行。当执行到这里时，main 函数调用 func 函数。此时，main 函数为调用函数，func 函数为被调函数。func 函数执行完后会返回 main 函数。

main 函数又是被操作系统调用的，操作系统加载一个程序后，会把程序中的 main 函数当作入口点，此时操作系统是调用者，main 函数是被调用者。main 函数执行完后，会返回值给操作系统，由操作系统继续接管。

## 函数的识别

函数既然要返回， 就需要返回地址的信息。在调用函数的代码中，保存了一个地址，该地址会与参数一起（如果有的话）传递给被调函数。通常编译器会使用 `call` 和 `ret` 指令来调用函数和返回。

call 指令与 `jmp` 之类的跳转指令类似，但 call 指令会保存返回地址的信息。在调用时， call 指令会将下一条指令的地址入栈，然后 ret 指令会将其出栈并跳转到这个地址。即 call 指令用于跳转到函数的起始地址并执行，ret 指令用于结束函数的执行并返回到原来的地址。

**因此可以通过定位 call 与 ret 指令来识别函数**。

考虑如下 C 代码：

```c
int sum(int a, int b) {
    return a + b;
}

int main(void) {
    printf("%d\n", sum(1, 2));

    return 0;
}
```

其汇编代码如下：

```assembly
; int sum(int a, int b)
00C11080  push        ebp
00C11081  mov         ebp,esp
; return a + b;
00C11083  mov         eax,dword ptr [ebp+8]
00C11086  add         eax,dword ptr [ebp+0Ch]
00C11089  pop         ebp
00C1108A  ret

; int main(void)
00C11090  push        ebp
00C11091  mov         ebp,esp
; printf("%d\n", sum(1, 2));
00C11093  push        2
00C11095  push        1
00C11097  call        00C11080                  ; sum 函数
00C1109C  add         esp,8
00C1109F  push        eax
00C110A0  push        0C13020h
00C110A5  call        00C11040
00C110AA  add         esp,8
; return 0;
00C110AD  xor         eax,eax
00C110AF  pop         ebp
00C110B0  ret
```

绝大部分函数调用的过程都是如此，还有一部分是间接调用——通过寄存器传递函数地址或计算偏移来动态计算函数地址，如：

```assembly
call [eax*4+10h]
```

对上述程序的 call 和 ret 指令来说，可以等效为如下指令：

```assembly
; call 指令等效
push 00C1109C   ; 将返回地址入栈
jmp 00C11080    ; 跳转到 sum 函数起始地址

; ret 指令等效
pop ecx         ; 由于 pop eip 指令是非法的，此处等效于将返回地址出栈
jmp ecx         ; 跳转到 call 指令的下一条指令
```

# 2 函数栈

## 栈帧

用栈来传递信息是最常见的方式。栈是一种**后进先出**的存储区，通常用栈来传递参数、存储返回信息、保存寄存器用于恢复，以及局部变量的存储，为单个过程分配的栈称为**栈帧**。

栈帧是程序栈的一段，它有一个起始地址，一个结束地址，而这两个地址分别存储在固定的寄存器中。x86 下，ebp 为**帧指针**，用于存放起始地址；esp 为**栈指针**，用于存放结束地址。由于栈指针始终指向栈顶，则用栈指针减去帧指针就可得出当前栈帧的大小 。由于栈是向低地址方向增长，**因此如果将栈指针减去一定的值，就相当于给栈帧分配内存**。

![栈帧结构](https://raw.githubusercontent.com/genskyff/image-hosting/main/images/202109202226539.png)

栈指针会在程序运行时移动，移动的栈指针无法根据偏移量准确的定位一个存储位置，因此大多数信息的访问都是基于帧指针的，但编译器有时会优化成直接使用栈指针来访问，这样就节省了一个寄存器。

栈帧提供了一种机制，每次函数调用都有它自己私有状态的信息存储，如保存的返回值、栈指针和被调用者保存寄存器的值，并提供局部变量的存储，局部变量在调用时分配内存，调用结束释放内存。每个调用在栈中都有自己的栈帧，多个未完成调用的其内部信息互不影响，分配和释放的栈与函数调用和返回的顺序相匹配。

## 栈平衡

当调用函数时，会根据需要申请栈空间，调用完成时，会释放栈空间，需要保证栈顶位置与调用前的位置相同，这个释放栈空间的过程被称为**栈平衡**。栈空间会被各种函数重复利用，如果只申请不释放，栈空间很快就会消耗完。

在调用函数时，需要依次把参数入栈，然后再调用函数执行。函数执行时，从栈中取得数据进行计算，执行结束后，由调用函数或被调函数修改栈，使栈平衡。

在 x64 下，某些汇编指令对栈顶的对齐有要求，在申请栈空间时，会保证栈顶地址能被 16 整除，以及在 Windows 上会有预留栈空间用来保存局部变量，因此会有申请了栈空间却不使用的情况。

# 3 x86 调用约定

当传递的参数大于 1 个时，传递的顺序就非常重要，这种在程序中为了实现函数调用而建立的协议被称为**调用约定**。这种协议规定了函数参数的传递方式、栈平衡由谁处理等问题。不同的语言、不同的编译器实现有着不同的调用约定。下面是一些常用调用约定：

| 约定类型     | \_\_cdecl（C 规范） | \_\_stdcall（Win32 规范） | \_\_fastcall |
| ------------ | ------------------- | ------------------------- | ------------ |
| 参数传递顺序 | 从右到左            | 从右到左                  | 寄存器和栈   |
| 平衡栈者     | 调用函数            | 被调函数                  | 被调函数     |
| 可变参数     | 是                  | 是                        |              |

> 注：\_\_stdcall 若为可变参数，则由调用函数平衡栈。

## \_\_cdecl 和 \_\_stdcall

\_\_cdecl 是 C 语言的默认调用约定，参数以从右到左的顺序传递，由调用函数平衡栈。\_\_stdcall 是 Windows API 的默认调用约定，由被调函数平衡栈，其余和 \_\_cdecl 基本相同。

考虑如下 C 代码：

```c
int sum(int x, int y) {
    int r = x + y;

    return r;
}

int main(void) {
    int a = 1, b = 2;
    printf("%d\n", sum(a, b));

    return 0;
}
```

分别使用 \_\_cdecl 和 \_\_stdcall 调用约定，其汇编代码如下：

`__cdecl`：

```assembly
; int sum(int x, int y)
00311080  push        ebp                       ; 保护 ebp
00311081  mov         ebp,esp                   ; 建立新的栈帧
00311083  push        ecx                       ; 分配内存
; int r = x + y;
00311084  mov         eax,dword ptr [ebp+8]
00311087  add         eax,dword ptr [ebp+0Ch]
0031108A  mov         dword ptr [ebp-4],eax
; return r;
0031108D  mov         eax,dword ptr [ebp-4]
00311090  mov         esp,ebp                   ; 释放内存
00311092  pop         ebp                       ; 恢复 ebp
00311093  ret

; int main(void)
003110A0  push        ebp
003110A1  mov         ebp,esp
003110A3  sub         esp,8                     ; 分配内存
; int a = 1, b = 2;
003110A6  mov         dword ptr [ebp-8],1
003110AD  mov         dword ptr [ebp-4],2
; printf("%d\n", sum(a, b));
003110B4  mov         eax,dword ptr [ebp-4]
003110B7  push        eax                       ; b 入栈
003110B8  mov         ecx,dword ptr [ebp-8]
003110BB  push        ecx                       ; a 入栈
003110BC  call        00311080                  ; sum 函数
003110C1  add         esp,8                     ; main 函数平衡栈
003110C4  push        eax
003110C5  push        313020h
003110CA  call        00311040
003110CF  add         esp,8
; return 0;
003110D2  xor         eax,eax
003110D4  mov         esp,ebp
003110D6  pop         ebp
003110D7  ret
```

`__stdcall`：

```assembly
; int sum(int x, int y)
00F61080  push        ebp
00F61081  mov         ebp,esp
00F61083  push        ecx
; int r = x + y;
00F61084  mov         eax,dword ptr [ebp+8]
00F61087  add         eax,dword ptr [ebp+0Ch]
00F6108A  mov         dword ptr [ebp-4],eax
; return r;
00F6108D  mov         eax,dword ptr [ebp-4]
00F61090  mov         esp,ebp
00F61092  pop         ebp
00F61093  ret         8                         ; 被调函数平衡栈

; int main(void)
00F610A0  push        ebp
00F610A1  mov         ebp,esp
00F610A3  sub         esp,8
; int a = 1, b = 2;
00F610A6  mov         dword ptr [ebp-8],1
00F610AD  mov         dword ptr [ebp-4],2
; printf("%d\n", sum(a, b));
00F610B4  mov         eax,dword ptr [ebp-4]
00F610B7  push        eax
00F610B8  mov         ecx,dword ptr [ebp-8]
00F610BB  push        ecx
00F610BC  call        00F61080                  ; sum 函数
00F610C1  push        eax
00F610C2  push        0F63020h
00F610C7  call        00F61040
00F610CC  add         esp,8
; return 0;
00F610CF  xor         eax,eax
00F610D1  mov         esp,ebp
00F610D3  pop         ebp
00F610D4  ret
```

以上两种方式，除了栈平衡，其他过程基本相同。

## \_\_fastcall

此约定还未被标准化，不同编译器的实现也不一致。如 MSVC 编译器规范为：前两个整型参数分别用 ecx 和 edx 传递，其余的按照从右到左的顺序传递，由被调函数平衡栈，**且浮点数、远指针、\_\_int64、结构体等只用栈传递**。

考虑如下 C 代码：

```c
int sum(float a, int b, float c, int d) {
    return a + b + c + d;
}

int main(void) {
    printf("%d\n", sum(1.1, 2, 3.3, 4));

    return 0;
}
```

使用 \_\_fastcall 调用约定，其汇编代码如下：

```assembly
; int sum(float a, int b, float c, int d)
00321080  push        ebp
00321081  mov         ebp,esp
00321083  sub         esp,8
00321086  mov         dword ptr [ebp-8],edx
00321089  mov         dword ptr [ebp-4],ecx
; return a + b + c + d;
0032108C  cvtsi2ss    xmm0,dword ptr [ebp-4]
00321091  addss       xmm0,dword ptr [ebp+8]
00321096  addss       xmm0,dword ptr [ebp+0Ch]
0032109B  cvtsi2ss    xmm1,dword ptr [ebp-8]
003210A0  addss       xmm0,xmm1
003210A4  cvttss2si   eax,xmm0
003210A8  mov         esp,ebp
003210AA  pop         ebp
003210AB  ret         8                             ; 被调函数平衡栈

; int main(void)
003210B0  push        ebp
003210B1  mov         ebp,esp
; printf("%d\n", sum(1.1, 2, 3.3, 4));
003210B3  push        ecx
003210B4  movss       xmm0,dword ptr ds:[00322104h]
003210BC  movss       dword ptr [esp],xmm0
003210C1  push        ecx
003210C2  movss       xmm0,dword ptr ds:[00322100h]
003210CA  movss       dword ptr [esp],xmm0
003210CF  mov         edx,4                         ; 第 2 个整型参数
003210D4  mov         ecx,2                         ; 第 1 个整型参数
003210D9  call        00321080
003210DE  push        eax
003210DF  push        323020h
003210E4  call        00321040
003210E9  add         esp,8
; return 0;
003210EC  xor         eax,eax
003210EE  pop         ebp
003210EF  ret
```

## \_\_thiscall

`__thiscall` 是 C++ 中非静态类成员函数的默认调用约定，对象的每个函数隐式接收 `this` 参数，this 指针为默认的第一个参数，由 ecx 传递，其余的按照从右到左的顺序传递，由被调函数平衡栈。当为可变参数时，由调用函数平衡栈。

考虑如下 C++ 代码：

```c++
class CNum {
public:
    int Sum(int a, int b) {
        return a + b;
    }
};

int main() {
    CNum cn;
    std::cout << cn.Sum(1, 2);

    return 0;
}
```

其汇编代码如下：

```assembly
; class CNum
; public:
; int Sum(int a, int b)
00261003  push        ecx
00261004  mov         dword ptr [ebp-4],ecx
; return a + b;
00261007  mov         eax,dword ptr [ebp+8]
0026100A  add         eax,dword ptr [ebp+0Ch]
0026100D  mov         esp,ebp
0026100F  pop         ebp
00261010  ret         8                             ; 被调函数平衡栈

; int main()
00261020  push        ebp
00261021  mov         ebp,esp
00261023  push        ecx
; CNum cn;
; std::cout << cn.Sum(1, 2);
00261024  push        2
00261026  push        1
00261028  lea         ecx,[ebp-1]                   ; this 指针
0026102B  call        00261000                      ; cn.Sum(1, 2)
00261030  push        eax
00261031  mov         ecx,dword ptr ds:[00262038h]
00261037  call        dword ptr ds:[00262034h]      ; std::cout
; return 0;
0026103D  xor         eax,eax
0026103F  mov         esp,ebp
00261041  pop         ebp
00261042  ret
```

## enter 和 leave

`enter` 和 `leave` 指令可以帮助进行栈的维护。

```assembly
; enter 指令等效
push ebp
mov ebp,esp
sub esp,imm

; leave 指令等效
add esp,imm
pop ebp
```

在函数中，等效于：

```assembly
; 函数
enter imm,0      ; 保护现场，为局部变量分配 imm 空间
; ...
leave            ; 恢复现场，释放局部变量
ret
```

## 返回值

函数执行后，会向调用者返回 1 个或多个执行结果，称为返回值，通常用 `return` 关键字来返回。可以通过传值或指针等方式来返回。

### return 返回

返回值会放在 eax 中，如果返回大小超过 eax 的大小，则高 32 位会放到 edx 中。如果是浮点数，则用 xmm0 返回。更大的返回值由调用函数在栈上分配空间，由 eax 保存该空间的指针并传递给被调用函数，调用结束后，eax 返回该空间的指针。

考虑如下 C 代码：

```c
typedef struct {
    int a;
    int b;
    int c;
} T;

T newT(int a, int b, int c) {
    T t = { a, b, c };
    return t;
}

int main(void) {
    T a = newT(1, 2, 3);

    return 0;
}
```

其汇编代码如下：

```assembly
; T newT(int a, int b, int c)
007A1003  sub         esp,0Ch
; T t = { a, b, c };
007A1006  mov         eax,dword ptr [ebp+0Ch]
007A1009  mov         dword ptr [ebp-0Ch],eax
007A100C  mov         ecx,dword ptr [ebp+10h]
007A100F  mov         dword ptr [ebp-8],ecx
007A1012  mov         edx,dword ptr [ebp+14h]
007A1015  mov         dword ptr [ebp-4],edx
; return t;
007A1018  mov         eax,dword ptr [ebp+8]
007A101B  mov         ecx,dword ptr [ebp-0Ch]
007A101E  mov         dword ptr [eax],ecx
007A1020  mov         edx,dword ptr [ebp-8]
007A1023  mov         dword ptr [eax+4],edx
007A1026  mov         ecx,dword ptr [ebp-4]
007A1029  mov         dword ptr [eax+8],ecx
007A102C  mov         eax,dword ptr [ebp+8]     ; 返回值首地址
007A102F  mov         esp,ebp
007A1031  pop         ebp
007A1032  ret

; int main(void)
007A1040  push        ebp
007A1041  mov         ebp,esp
007A1043  sub         esp,18h                   ; 分配返回值的栈空间
; T a = newT(1, 2, 3);
007A1046  push        3
007A1048  push        2
007A104A  push        1
007A104C  lea         eax,[ebp-18h]
007A104F  push        eax                       ; 用 eax 传递返回值的地址
007A1050  call        007A1000
007A1055  add         esp,10h
007A1058  mov         ecx,dword ptr [eax]
007A105A  mov         dword ptr [ebp-0Ch],ecx
007A105D  mov         edx,dword ptr [eax+4]
007A1060  mov         dword ptr [ebp-8],edx
007A1063  mov         eax,dword ptr [eax+8]
007A1066  mov         dword ptr [ebp-4],eax
; return 0;
007A1069  xor         eax,eax
007A106B  mov         esp,ebp
007A106D  pop         ebp
007A106E  ret
```

可以看到，该结构体大于 8 字节，调用函数分配返回值的空间，并将首地址通过 eax 传递。

### 指针返回

给函数传递参数有两种，分别是传值和指针。传值调用时，会建立参数的副本，被调函数在其内部对参数进行操作，不会对调用函数中的原始变量造成影响。传指针时允许被调函数修改调用函数中的原始变量。

考虑如下 C 代码：

```c
void func(int *a, int *b) {
    if (*a < *b)
        *a = *b;
}

int main(void) {
    int a = 1, b = 2;
    func(&a, &b);

    return 0;
}
```

其汇编代码如下：

```assembly
; void func(int *a, int *b)
00AB1080  push        ebp
00AB1081  mov         ebp,esp
; if (*a < *b)
00AB1083  mov         eax,dword ptr [ebp+8]     ; &a
00AB1086  mov         ecx,dword ptr [ebp+0Ch]   ; &b
00AB1089  mov         edx,dword ptr [eax]
00AB108B  cmp         edx,dword ptr [ecx]
00AB108D  jge         00AB1099
; *a = *b;
00AB108F  mov         eax,dword ptr [ebp+8]
00AB1092  mov         ecx,dword ptr [ebp+0Ch]
00AB1095  mov         edx,dword ptr [ecx]
00AB1097  mov         dword ptr [eax],edx
00AB1099  pop         ebp
00AB109A  ret

; int main(void)
00AB10A0  push        ebp
00AB10A1  mov         ebp,esp
00AB10A3  sub         esp,8
; int a = 1, b = 2;
00AB10A6  mov         dword ptr [ebp-8],1
00AB10AD  mov         dword ptr [ebp-4],2
; func(&a, &b);
00AB10B4  lea         eax,[ebp-4]
00AB10B7  push        eax
00AB10B8  lea         ecx,[ebp-8]
00AB10BB  push        ecx
00AB10BC  call        00AB1080
00AB10C1  add         esp,8
; printf("%d, %d\n", a, b);
00AB10C4  mov         edx,dword ptr [ebp-4]
00AB10C7  push        edx
00AB10C8  mov         eax,dword ptr [ebp-8]
00AB10CB  push        eax
00AB10CC  push        0AB3020h
00AB10D1  call        00AB1040
00AB10D6  add         esp,0Ch
; return 0;
00AB10D9  xor         eax,eax
00AB10DB  mov         esp,ebp
00AB10DD  pop         ebp
00AB10DE  ret
```

## 寄存器使用惯例

寄存器是唯一能够被所有函数共享的资源。虽然在某一时刻只能有一个函数是活动的，但是必须保证当一个函数调用另一个函数时，被调函数内部使用的寄存器不会覆盖调用它的函数稍后会使用的寄存器的值，因此必须采用统一的寄存器使用惯例，所有的函数都必须遵守，包括程序的库。

若没有这些惯例，如在调用函数时，无论是调用函数还是被调函数，都可能更新寄存器的值。如调用函数在 ebx 中存了一个值准备在函数调用完毕后使用，而被调函数也使用了这个寄存器，并更新了值，于是调用函数再次使用 ebx 的时候，就已经不是原来的那个值了，接下来的执行很可能出现错误。

在 x86 中，寄存器 eax、ecx 和 edx 被划分为**调用函数保存寄存器**。当函数 P 调用函数 Q 时，Q 可以更新这些寄存器，而不会破坏 P 所需的值。寄存器 ebx、esi 和 edi 被划分为**被调函数保存寄存器**。这里 Q 在更新这些寄存器的值之前，必须先将其保存到栈中，并在返回前恢复，因为 P 可能会在之后使用这些值。

# 4 x64 调用约定

x64 下只有 \_\_fastcall 这一种调用约定，但 Windows 和 Linux、macOS 等遵循 System V ABI 的系统的调用约定是不同的，因此需要分开讨论。

## Windows

### 调用约定

- **前 4 个参数使用寄存器传递**，其余的参数用栈传递，入栈顺序从右到左，由调用函数平衡栈
- **前 4 个参数使用的寄存器是固定的，第 1 个是 rcx，第 2 个是 rdx，第 3 个是 r8，第 4 个是 r9**
- **浮点数按顺序用 xmm0 ~ xmm3 寄存器传递**
- 少于 64 位的参数不做零扩展，此时高位保持原来的值
- 任何大于 8 字节或不是 1、2、4、8 字节的参数必须用栈来传递
- 栈需要 16 字节对齐
- **无论参数个数多少，函数都会在开头分配 32 字节的预留栈空间，在调用结束后释放**
- **默认不使用帧指针 rbp，所有的栈操作都通过 rsp 来完成**
- **当返回值为不大于 64 位的整型或指针时，用 rax 返回**
- 若返回值是浮点型，则用 xmm0 返回
- 更大的返回值由调用函数在栈上分配空间，**由 rcx 保存该空间的指针并传递给被调用函数，此时 rcx 不能被用作参数传递**，调用完毕后，rax 返回该空间的指针
- **除了用于传递参数的 rcx、rdx、r8 和 r9**，用于返回值的 rax，以及 r10 和 r11 不需要保护外，其余的寄存器在被调函数使用前需要保护

|     参数     | 整型 | 浮点型 |
| :----------: | ---- | ------ |
| 第 1 个参数  | rcx  | xmm0   |
| 第 2 个参数  | rdx  | xmm1   |
| 第 3 个参数  | r8   | xmm2   |
| 第 4 个参数  | r9   | xmm3   |
| 第 5 个参数+ | 栈   | 栈     |

虽然前四个参数用寄存器传递，但是栈依然会预留 32 字节的空间，称为**预留栈空间**。由于使用了这 4 个寄存器，因此函数内部就不能再使用这些寄存器，当函数功能比较复杂时，寄存器可能不够用，此时可以用预留栈空间来保存这些参数。参数按照从左到右的顺序，保存在预留栈空间的低地址到高地址。

![Windows x64 栈帧](https://raw.githubusercontent.com/genskyff/image-hosting/main/images/202109250128482.png)

### 多种参数类型

若参数既有整型又有浮点型，那么无论整型和浮点型各多少个，只要总数大于 4 个，那么剩下的都用栈传递，前 4 个按照对应的寄存器传递。

考虑如下 C 代码：

```c
void func(int i1, double d1, int i2, double d2, int i3, double d3) {
    printf("%d %d %d\n", i1, i2, i3);
    printf("%f %f %f\n", d1, d2, d3);
}

int main(void) {
    func(1, 2.2, 3, 4.4, 5, 6.6);

    return 0;
}
```

其 x64 汇编代码如下：

```assembly
; void func(int i1, double d1, int i2, double d2, int i3, double d3)
; 将 4 个参数保存到预留栈空间
00007FF7527210C0  movsd       mmword ptr [rsp+20h],xmm3
00007FF7527210C6  mov         dword ptr [rsp+18h],r8d
00007FF7527210CB  movsd       mmword ptr [rsp+10h],xmm1
00007FF7527210D1  mov         dword ptr [rsp+8],ecx
00007FF7527210D5  sub         rsp,28h
; printf("%d %d %d\n", i1, i2, i3);
00007FF7527210D9  mov         r9d,dword ptr [rsp+50h]
00007FF7527210DE  mov         r8d,dword ptr [rsp+40h]
00007FF7527210E3  mov         edx,dword ptr [rsp+30h]
00007FF7527210E7  lea         rcx,[00007FF752723048h]
00007FF7527210EE  call        00007FF752721060
; printf("%f %f %f\n", d1, d2, d3);
00007FF7527210F3  movsd       xmm3,mmword ptr [rsp+58h]
00007FF7527210F9  movq        r9,xmm3
00007FF7527210FE  movsd       xmm2,mmword ptr [rsp+48h]
00007FF752721104  movq        r8,xmm2
00007FF752721109  movsd       xmm1,mmword ptr [rsp+38h]
00007FF75272110F  movq        rdx,xmm1
00007FF752721114  lea         rcx,[00007FF752723038h]
00007FF75272111B  call        00007FF752721060
00007FF752721120  add         rsp,28h
00007FF752721124  ret

; int main(void)
00007FF752721130  sub         rsp,38h
; func(1, 2.2, 3, 4.4, 5, 6.6);
00007FF752721134  movsd       xmm0,mmword ptr [00007FF752722230h]
00007FF75272113C  movsd       mmword ptr [rsp+28h],xmm0             ; 第 6 个参数
00007FF752721142  mov         dword ptr [rsp+20h],5                 ; 第 5 个参数
00007FF75272114A  movsd       xmm3,mmword ptr [00007FF752722228h]   ; 第 4 个参数
00007FF752721152  mov         r8d,3                                 ; 第 3 个参数
00007FF752721158  movsd       xmm1,mmword ptr [00007FF752722220h]   ; 第 2 个参数
00007FF752721160  mov         ecx,1                                 ; 第 1 个参数
00007FF752721165  call        00007FF7527210C0
; return 0;
00007FF75272116A  xor         eax,eax
00007FF75272116C  add         rsp,38h
00007FF752721170  ret
```

可以看到，第 1 个参数用 rcx，第 2 个参数用 xmm1，第 3 个参数用 r8，第 4 个参数用 xmm3，并保存在预留栈空间中，剩下两个用栈传递，用栈传递不一定会使用 push 指令，也不一定会使用帧指针，而是直接使用栈指针加偏移。

## System V ABI

### 调用约定

- **前 6 个参数使用寄存器传递**，其余的参数用栈传递，入栈顺序从右到左，由调用函数平衡栈
- **前 6 参数使用的寄存器是固定的，第 1 个是 rdi，第 2 个是 rsi，第 3 个是 rdx，第 4 个是 rcx，第 5 个是 r8，第 6 个是 r9**
- **对于系统调用，用 r10 替代 rcx**
- **浮点数按顺序用 xmm0 ~ xmm7 寄存器传递**
- 少于 64 位的参数不做零扩展，此时高位保持原来的值
- 任何大于 8 字节或不是 1、2、4、8 字节的参数必须用栈来传递
- 栈需要 16 字节对齐
- **无预留栈空间**
- **默认使用帧指针 rbp**
- **当返回值为不大于 64 位的整型或指针时，用 rax 返回，否则高 64 位会放到 rdx 中**
- 若返回值是浮点型，则用 xmm0 返回
- 更大的返回值由调用函数在栈上分配空间，**由 rdi 保存该空间的指针并传递给被调用函数，此时 rdi 不能被用作常规参数传递**，调用完毕后，rax 返回该空间的指针
- **除了用于传递参数的 rdi、rsi、rdx、rcx、r8 和 r9**，用于返回值的 rax，以及 r10 和 r11 不需要保护外，其余的寄存器在被调函数使用前需要保护

|     参数     | 整型 | 浮点型 |
| :----------: | :--- | ------ |
| 第 1 个参数  | rdi  | xmm0   |
| 第 2 个参数  | rsi  | xmm1   |
| 第 3 个参数  | rdx  | xmm2   |
| 第 4 个参数  | rcx  | xmm3   |
| 第 5 个参数  | r8   | xmm4   |
| 第 6 个参数  | r9   | xmm5   |
| 第 7 个参数  | 栈   | xmm6   |
| 第 8 个参数  | 栈   | xmm7   |
| 第 9 个参数+ | 栈   | 栈     |

![Linux x64 栈帧](https://raw.githubusercontent.com/genskyff/image-hosting/main/images/202109250130369.png)

### 多种参数类型

和 Windows 不同，若参数既有整型又有浮点型，那么只有当整型参数超过 6 个或浮点型超过 8 个时，才会用栈传递。

考虑如下 C 代码：

```c
void func(int i1, int i2, int i3, int i4, int i5, int i6,
    double d1, double d2, double d3, double d4,
    double d5, double d6, double d7, double d8,
    int i7) {
    printf("%d %d %d %d %d %d\n", i1, i2, i3, i4, i5, i6);
    printf("%f %f %f %f %f %f %f %f\n", d1, d2, d3, d4, d5, d6, d7, d8);
    printf("%d\n", i7);
}

int main(void) {
    func(1, 2, 3, 4, 5, 6,
        1.1, 2.2, 3.3, 4.4, 5.5, 6.6, 7.7, 8.8,
        7);

    return 0;
}
```

在 Linux x64 下，使用 gcc 默认设置编译，用 IDA 反汇编后，其代码如下：

```assembly
.text:0000000000001135 public func
.text:0000000000001135 func proc near                ; CODE XREF: main+8A↓p
.text:0000000000001135
.text:0000000000001135 var_58= qword ptr -58h
.text:0000000000001135 var_50= qword ptr -50h
.text:0000000000001135 var_48= qword ptr -48h
.text:0000000000001135 var_40= qword ptr -40h
.text:0000000000001135 var_38= qword ptr -38h
.text:0000000000001135 var_30= qword ptr -30h
.text:0000000000001135 var_28= qword ptr -28h
.text:0000000000001135 var_20= qword ptr -20h
.text:0000000000001135 var_18= dword ptr -18h
.text:0000000000001135 var_14= dword ptr -14h
.text:0000000000001135 var_10= dword ptr -10h
.text:0000000000001135 var_C= dword ptr -0Ch
.text:0000000000001135 var_8= dword ptr -8
.text:0000000000001135 var_4= dword ptr -4
.text:0000000000001135 arg_0= dword ptr  10h
.text:0000000000001135
.text:0000000000001135 ; __unwind {
.text:0000000000001135 push    rbp
.text:0000000000001136 mov     rbp, rsp
.text:0000000000001139 sub     rsp, 60h
.text:000000000000113D mov     [rbp+var_4], edi
.text:0000000000001140 mov     [rbp+var_8], esi
.text:0000000000001143 mov     [rbp+var_C], edx
.text:0000000000001146 mov     [rbp+var_10], ecx
.text:0000000000001149 mov     [rbp+var_14], r8d
.text:000000000000114D mov     [rbp+var_18], r9d
.text:0000000000001151 movsd   [rbp+var_20], xmm0
.text:0000000000001156 movsd   [rbp+var_28], xmm1
.text:000000000000115B movsd   [rbp+var_30], xmm2
.text:0000000000001160 movsd   [rbp+var_38], xmm3
.text:0000000000001165 movsd   [rbp+var_40], xmm4
.text:000000000000116A movsd   [rbp+var_48], xmm5
.text:000000000000116F movsd   [rbp+var_50], xmm6
.text:0000000000001174 movsd   [rbp+var_58], xmm7
.text:0000000000001179 mov     r8d, [rbp+var_14]
.text:000000000000117D mov     edi, [rbp+var_10]
.text:0000000000001180 mov     ecx, [rbp+var_C]
.text:0000000000001183 mov     edx, [rbp+var_8]
.text:0000000000001186 mov     eax, [rbp+var_4]
.text:0000000000001189 sub     rsp, 8
.text:000000000000118D mov     esi, [rbp+var_18]
.text:0000000000001190 push    rsi
.text:0000000000001191 mov     r9d, r8d
.text:0000000000001194 mov     r8d, edi
.text:0000000000001197 mov     esi, eax
.text:0000000000001199 lea     rdi, format           ; "%d %d %d %d %d %d\n"
.text:00000000000011A0 mov     eax, 0
.text:00000000000011A5 call    _printf
.text:00000000000011AA add     rsp, 10h
.text:00000000000011AE movsd   xmm6, [rbp+var_58]
.text:00000000000011B3 movsd   xmm5, [rbp+var_50]
.text:00000000000011B8 movsd   xmm4, [rbp+var_48]
.text:00000000000011BD movsd   xmm3, [rbp+var_40]
.text:00000000000011C2 movsd   xmm2, [rbp+var_38]
.text:00000000000011C7 movsd   xmm1, [rbp+var_30]
.text:00000000000011CC movsd   xmm0, [rbp+var_28]
.text:00000000000011D1 mov     rax, [rbp+var_20]
.text:00000000000011D5 movapd  xmm7, xmm6
.text:00000000000011D9 movapd  xmm6, xmm5
.text:00000000000011DD movapd  xmm5, xmm4
.text:00000000000011E1 movapd  xmm4, xmm3
.text:00000000000011E5 movapd  xmm3, xmm2
.text:00000000000011E9 movapd  xmm2, xmm1
.text:00000000000011ED movapd  xmm1, xmm0
.text:00000000000011F1 movq    xmm0, rax
.text:00000000000011F6 lea     rdi, aFFFFFFFF        ; "%f %f %f %f %f %f %f %f\n"
.text:00000000000011FD mov     eax, 8
.text:0000000000001202 call    _printf
.text:0000000000001207 mov     esi, [rbp+arg_0]
.text:000000000000120A lea     rdi, aD               ; "%d\n"
.text:0000000000001211 mov     eax, 0
.text:0000000000001216 call    _printf
.text:000000000000121B nop
.text:000000000000121C leave
.text:000000000000121D retn
.text:000000000000121D ; } // starts at 1135
.text:000000000000121D func endp

; ----------------------------------------------------------------------------

.text:000000000000121E ; int __cdecl main(int argc, const char **argv, const char **envp)
.text:000000000000121E public main
.text:000000000000121E main proc near                ; DATA XREF: _start+1D↑o
.text:000000000000121E ; __unwind {
.text:000000000000121E push    rbp
.text:000000000000121F mov     rbp, rsp
.text:0000000000001222 movsd   xmm6, cs:qword_2038
.text:000000000000122A movsd   xmm5, cs:qword_2040
.text:0000000000001232 movsd   xmm4, cs:qword_2048
.text:000000000000123A movsd   xmm3, cs:qword_2050
.text:0000000000001242 movsd   xmm2, cs:qword_2058
.text:000000000000124A movsd   xmm1, cs:qword_2060
.text:0000000000001252 movsd   xmm0, cs:qword_2068
.text:000000000000125A mov     rax, cs:qword_2070
.text:0000000000001261 sub     rsp, 8
.text:0000000000001265 push    7                     ; 最后一个参数用栈传递
.text:0000000000001267 movapd  xmm7, xmm6
.text:000000000000126B movapd  xmm6, xmm5
.text:000000000000126F movapd  xmm5, xmm4
.text:0000000000001273 movapd  xmm4, xmm3
.text:0000000000001277 movapd  xmm3, xmm2
.text:000000000000127B movapd  xmm2, xmm1
.text:000000000000127F movapd  xmm1, xmm0
.text:0000000000001283 movq    xmm0, rax
.text:0000000000001288 mov     r9d, 6
.text:000000000000128E mov     r8d, 5
.text:0000000000001294 mov     ecx, 4
.text:0000000000001299 mov     edx, 3
.text:000000000000129E mov     esi, 2
.text:00000000000012A3 mov     edi, 1
.text:00000000000012A8 call    func
.text:00000000000012AD add     rsp, 10h
.text:00000000000012B1 mov     eax, 0
.text:00000000000012B6 leave
.text:00000000000012B7 retn
.text:00000000000012B7 ; } // starts at 121E
.text:00000000000012B7 main endp
```

可以看到，即使超过了 6 个参数，但是只有当整型或浮点型都超过相应的个数时，才会使用栈传递，且没有预留栈空间。

## \_\_thiscall

C++ 中，this 指针始终隐式的被当作第一个参数，在 Windows 下用 rcx 传递，System V ABI 用 rdi 传递，其余的按照规范传递。

考虑如下 C++ 代码：

```c++
class CNum {
public:
    int Sum(int a, int b) {
        return a + b;
    }
};

int main() {
    CNum cn;
    printf("%d\n", cn.Sum(1, 2));

    return 0;
}
```

其 x64 汇编代码如下：

```assembly
; class CNum
; public:
; int Sum(int a, int b)
00007FF605861000  mov         dword ptr [rsp+18h],r8d
00007FF605861005  mov         dword ptr [rsp+10h],edx
00007FF605861009  mov         qword ptr [rsp+8],rcx
; return a + b;
00007FF60586100E  mov         eax,dword ptr [rsp+18h]
00007FF605861012  mov         ecx,dword ptr [rsp+10h]
00007FF605861016  add         ecx,eax
00007FF605861018  mov         eax,ecx
00007FF60586101A  ret

; int main()
00007FF605861020  sub         rsp,38h
; CNum cn;
; std::cout << cn.Sum(1, 2);
00007FF605861024  mov         r8d,2
00007FF60586102A  mov         edx,1
00007FF60586102F  lea         rcx,[rsp+20h]                     ; this 指针
00007FF605861034  call        00007FF605861000                  ; cn.Sum(1, 2)
00007FF605861039  mov         edx,eax
00007FF60586103B  mov         rcx,qword ptr [00007FF605862078h]
00007FF605861042  call        qword ptr [00007FF605862070h]     ; std::cout
; return 0;
00007FF605861048  xor         eax,eax
00007FF60586104A  add         rsp,38h
00007FF60586104E  ret
```

在 Linux x64 下，使用 g++ 默认设置编译，用 IDA 反汇编后，其代码如下：

```assembly
.text:00000000000011E6 ; __int64 __fastcall CNum::Sum(CNum *__hidden this, int, int)
.text:00000000000011E6 public _ZN4CNum3SumEii ; weak
.text:00000000000011E6 _ZN4CNum3SumEii proc near     ; CODE XREF: main+19↑p
.text:00000000000011E6
.text:00000000000011E6 var_10= dword ptr -10h
.text:00000000000011E6 var_C= dword ptr -0Ch
.text:00000000000011E6 var_8= qword ptr -8
.text:00000000000011E6
.text:00000000000011E6 ; __unwind {
.text:00000000000011E6 push    rbp
.text:00000000000011E7 mov     rbp, rsp
.text:00000000000011EA mov     [rbp+var_8], rdi
.text:00000000000011EE mov     [rbp+var_C], esi
.text:00000000000011F1 mov     [rbp+var_10], edx
.text:00000000000011F4 mov     edx, [rbp+var_C]
.text:00000000000011F7 mov     eax, [rbp+var_10]
.text:00000000000011FA add     eax, edx
.text:00000000000011FC pop     rbp
.text:00000000000011FD retn
.text:00000000000011FD ; } // starts at 11E6
.text:00000000000011FD _ZN4CNum3SumEii endp

; -----------------------------------------------------------------------------

.text:0000000000001155 ; int __cdecl main(int argc, const char **argv, const char **envp)
.text:0000000000001155 public main
.text:0000000000001155 main proc near                ; DATA XREF: _start+1D↑o
.text:0000000000001155
.text:0000000000001155 var_1= byte ptr -1
.text:0000000000001155
.text:0000000000001155 ; __unwind {
.text:0000000000001155 push    rbp
.text:0000000000001156 mov     rbp, rsp
.text:0000000000001159 sub     rsp, 10h
.text:000000000000115D lea     rax, [rbp+var_1]
.text:0000000000001161 mov     edx, 2                ; int
.text:0000000000001166 mov     esi, 1                ; int
.text:000000000000116B mov     rdi, rax              ; this 指针
.text:000000000000116E call    _ZN4CNum3SumEii       ; CNum::Sum(int,int)
.text:0000000000001173 mov     esi, eax
.text:0000000000001175 lea     rdi, __bss_start
.text:000000000000117C call    __ZNSolsEi            ; std::ostream::operator<<(int)
.text:0000000000001181 mov     eax, 0
.text:0000000000001186 leave
.text:0000000000001187 retn
.text:0000000000001187 ; } // starts at 1155
.text:0000000000001187 main endp
```

## 参数为结构体

当为结构体时，参数大小可能不是 1、2、4、8 字节，也有可能超过 8 字节。

考虑如下 C 代码：

```c
struct Point2D {
    int x;
    int y;
};

struct Point3D {
    int x;
    int y;
    int z;
};

int sum2D(struct Point2D d) {
    return d.x + d.y;
}

int sum3D(struct Point3D d) {
    return d.x + d.y + d.z;
}

int main(void) {
    struct Point2D d1 = { 1, 2 };
    struct Point3D d2 = { 1, 2, 3 };
    printf("%d, %d\n", sum2D(d1), sum3D(d2));

    return 0;
}
```

其 x64 汇编代码如下：

```assembly
; int sum2D(struct Point2D d)
00007FF7CC3510C0  mov         qword ptr [rsp+8],rcx
; return d.x + d.y;
00007FF7CC3510C5  mov         eax,dword ptr [rsp+0Ch]
00007FF7CC3510C9  mov         ecx,dword ptr [rsp+8]
00007FF7CC3510CD  add         ecx,eax
00007FF7CC3510CF  mov         eax,ecx
00007FF7CC3510D1  ret

; int sum3D(struct Point3D d)
00007FF7CC3510E0  mov         qword ptr [rsp+8],rcx
; return d.x + d.y + d.z;
00007FF7CC3510E5  mov         rax,qword ptr [rsp+8]
00007FF7CC3510EA  mov         eax,dword ptr [rax]
00007FF7CC3510EC  mov         rcx,qword ptr [rsp+8]
00007FF7CC3510F1  add         eax,dword ptr [rcx+4]
00007FF7CC3510F4  mov         rcx,qword ptr [rsp+8]
00007FF7CC3510F9  add         eax,dword ptr [rcx+8]
00007FF7CC3510FC  ret

; int main(void)
00007FF7CC351100  push        rsi
00007FF7CC351102  push        rdi
00007FF7CC351103  sub         rsp,58h
; struct Point2D d1 = { 1, 2 };
00007FF7CC351107  mov         dword ptr [rsp+28h],1
00007FF7CC35110F  mov         dword ptr [rsp+2Ch],2
; struct Point3D d2 = { 1, 2, 3 };
00007FF7CC351117  mov         dword ptr [rsp+30h],1
00007FF7CC35111F  mov         dword ptr [rsp+34h],2
00007FF7CC351127  mov         dword ptr [rsp+38h],3
; printf("%d, %d\n", sum2D(d1), sum3D(d2));
00007FF7CC35112F  lea         rax,[rsp+40h]
00007FF7CC351134  lea         rcx,[rsp+30h]
00007FF7CC351139  mov         rdi,rax
00007FF7CC35113C  mov         rsi,rcx
00007FF7CC35113F  mov         ecx,0Ch
00007FF7CC351144  rep movs    byte ptr [rdi],byte ptr [rsi]     ; 将 d2 复制到首地址为 rdi 的栈空间中
00007FF7CC351146  lea         rcx,[rsp+40h]
00007FF7CC35114B  call        00007FF7CC3510E0                  ; sum3D(d2)
00007FF7CC351150  mov         dword ptr [rsp+20h],eax
00007FF7CC351154  mov         rcx,qword ptr [rsp+28h]           ; rcx = d1，ecx = d1.x，rcx 高 32 位 = d1.y
00007FF7CC351159  call        00007FF7CC3510C0                  ; sum2D(d1)
00007FF7CC35115E  mov         ecx,dword ptr [rsp+20h]
00007FF7CC351162  mov         r8d,ecx
00007FF7CC351165  mov         edx,eax
00007FF7CC351167  lea         rcx,[00007FF7CC352220h]
00007FF7CC35116E  call        00007FF7CC351060
; return 0;
00007FF7CC351173  xor         eax,eax
00007FF7CC351175  add         rsp,58h
00007FF7CC351179  pop         rdi
00007FF7CC35117A  pop         rsi
00007FF7CC35117B  ret
```

可以看到，如果结构体 ≤ 8 字节，则把整个结构体放入寄存器中，函数通过访问寄存器的高 32 位和低 32 位来访问结构体成员。如果大于 8 字节，在传递参数时，先把结构体复制到栈空间中，再把这个空间的首地址当作参数来传递，即通过指针传递，函数通过结构体地址加偏移来访问结构体成员。

# 5 回调函数

回调函数就是一个通过函数指针调用的函数。若把函数的指针作为参数传递给另一个函数，当这个指针被用来调用其所指向的函数时，则称该函数为**回调函数**。回调函数不是由该函数的实现方直接调用，而是在特定的事件或条件发生时由另外的一方调用的，用于对该事件或条件进行响应。

## 函数指针

一个指针指向的是函数的首地址，则称之为**函数指针**。每个函数都有一个入口地址，即首地址，根据这个地址，可以访问并使用这个函数。函数指针就是通过指向这个函数的入口，从而调用这个函数。

### 定义

有两种方式定义函数指针，如下：

```c
// 方式一
int (*pfunc)(int, double) = NULL;

// 方式二
typedef int (*TPFunc)(int, double);
TPFunc pfunc = NULL;
```

这两种方式都定义了一个返回值为 `int` 类型，参数为 `(int, double)` 的函数指针。

### 赋值

有两种方式给函数指针赋值，如下：

```c
// 方式一
pfunc = &func1;

// 方式二
pfunc = func2;
```

和数组名类似，函数名被看作这个函数的首地址。

### 调用

和通常的指针一样，函数指针也可以使用取值运算符 `*` 来调用，或者直接使用函数的方式，如下：

```c
// 方式一
int v1 = pfunc(1, 2.2);

// 方式二
int v2 = (*pfunc)(1, 2.2);
```

### 作为参数

可以把函数指针当作函数的参数传递给函数，如下：

```cpp
// func1 将函数指针 pfunc 作为其形参
void func1(void (*pfunc)(int, double), int a, double b) {
    (*pfunc)(a, b);
}

// func2 调用函数 func1
void func2(void) {
    func1(func, 1, 2.2);
}
```

### 作为返回值

可以把函数指针作为返回值，如下：

```c
void (*func(int, double))(int, int);
```

这是一个参数为 `(int, double)`，返回值为 `void (*)(int, int)` 的函数声明。

### 作为数组

可以用数组来存放函数指针，有两种方式，如下：

```c
// 方式一
void (*func[10])(int, double);

// 方式二
typedef void (*PFunc)(int, double);
PFunc func[10];
```

这两种方式都定义函数指针数组，它定义了一个元素个数为 10，类型是 `void (*)(int, double)` 的函数指针数组。

## 使用回调函数

以一个计算值的函数为例，向其传递函数指针，表示要计算的方式。

考虑如下 C 代码：

```c
int add(int x, int y) {
    return x + y;
}

int sub(int x, int y) {
    return x - y;
}

int calc(int (*pfunc)(int, int), int x, int y) {
    return pfunc(x, y);
}

int main(void) {
    int r1 = calc(add, 8, 2);
    int r2 = calc(sub, 8, 2);
    printf("%d, %d\n", r1, r2);

    return 0;
}
```

其汇编代码如下：

```assembly
; int add(int x, int y)
00F11080  push        ebp
00F11081  mov         ebp,esp
; return x + y;
00F11083  mov         eax,dword ptr [ebp+8]
00F11086  add         eax,dword ptr [ebp+0Ch]
00F11089  pop         ebp
00F1108A  ret

; int sub(int x, int y)
00F11090  push        ebp
00F11091  mov         ebp,esp
; return x - y;
00F11093  mov         eax,dword ptr [ebp+8]
00F11096  sub         eax,dword ptr [ebp+0Ch]
00F11099  pop         ebp
00F1109A  ret

; int calc(int (*pfunc)(int, int), int x, int y)
00F110A0  push        ebp
00F110A1  mov         ebp,esp
; return pfunc(x, y);
00F110A3  mov         eax,dword ptr [ebp+10h]
00F110A6  push        eax
00F110A7  mov         ecx,dword ptr [ebp+0Ch]
00F110AA  push        ecx
00F110AB  call        dword ptr [ebp+8]
00F110AE  add         esp,8
00F110B1  pop         ebp
00F110B2  ret

; int main(void)
00F110C0  push        ebp
00F110C1  mov         ebp,esp
00F110C3  sub         esp,8
; int r1 = calc(add, 8, 2);
00F110C6  push        2
00F110C8  push        8
00F110CA  push        0F11080h              ; add 函数地址
00F110CF  call        00F110A0
00F110D4  add         esp,0Ch
00F110D7  mov         dword ptr [ebp-8],eax
; int r2 = calc(sub, 8, 2);
00F110DA  push        2
00F110DC  push        8
00F110DE  push        0F11090h              ; sub 函数地址
00F110E3  call        00F110A0
00F110E8  add         esp,0Ch
00F110EB  mov         dword ptr [ebp-4],eax
; printf("%d, %d\n", r1, r2);
00F110EE  mov         eax,dword ptr [ebp-4]
00F110F1  push        eax
00F110F2  mov         ecx,dword ptr [ebp-8]
00F110F5  push        ecx
00F110F6  push        0F13020h
00F110FB  call        00F11040
00F11100  add         esp,0Ch
; return 0;
00F11103  xor         eax,eax
00F11105  mov         esp,ebp
00F11107  pop         ebp
00F11108  ret
```

可以看到，实际传递的参数为函数的地址，然后 call 指令调用这个地址，和一般的函数调用没有什么区别。

# 6 递归函数

一个函数能调用其他函数，也能调用自己，称为**递归调用**。

考虑如下 C 代码：

```c
int fact(int n) {
    int r;
    if (n <= 1)
        r = 1;
    else
        r = n * fact(n - 1);

    return r;
}

int main(void) {
    printf("%d\n", fact(5));

    return 0;
}
```

这是一个计算 n! 的递归调用代码，其汇编代码如下：

```assembly
; int fact(int n)
00A91080  push        ebp
00A91081  mov         ebp,esp
00A91083  push        ecx
; int r;
; if (n <= 1)
00A91084  cmp         dword ptr [ebp+8],1
00A91088  jg          00A91093
; r = 1;
00A9108A  mov         dword ptr [ebp-4],1
00A91091  jmp         00A910A9
; else
; r = n * fact(n - 1);
00A91093  mov         eax,dword ptr [ebp+8]
00A91096  sub         eax,1
00A91099  push        eax
00A9109A  call        00A91080              ; 调用自身，并创建新的栈帧
00A9109F  add         esp,4
00A910A2  imul        eax,dword ptr [ebp+8]
00A910A6  mov         dword ptr [ebp-4],eax
; return r;
00A910A9  mov         eax,dword ptr [ebp-4]
00A910AC  mov         esp,ebp
00A910AE  pop         ebp
00A910AF  ret

; int main(void)
00A910B0  push        ebp
00A910B1  mov         ebp,esp
; printf("%d\n", fact(5));
00A910B3  push        5
00A910B5  call        00A91080              ; fact(5)
00A910BA  add         esp,4
00A910BD  push        eax
00A910BE  push        0A92100h
00A910C3  call        00A91040
00A910C8  add         esp,8
; return 0;
00A910CB  xor         eax,eax
00A910CD  pop         ebp
00A910CE  ret
```

可以看到，当 n > 1 时，会不断地跳转到 fact 函数的首地址，并创立新的栈帧，虽然调用的是相同的函数，但栈帧却不同。直到满足 n <= 1 为止，栈帧都会继续扩大下去，栈地址会不断减小，因此如果递归层数太多，会造成栈持续增长，导致栈空间不足，因此有必要利用尾递归来优化递归函数。

## 尾调用

尾调用是指一个函数里最后一个动作是函数调用，即这个调用的返回值直接被当前函数的返回，该调用的位置称为**尾位置**。

考虑如下 C 代码：

```c
// 情况一
int func1(void) {
    int a = g();

    return a;
}

// 情况二
int func2(void) {
    return g() + 1;
}

// 情况三
int func3(int x) {
    if (x > 0)
        return g(x);

    return g(x);
}
```

- 情况一在调用函数之后，还有别的操作，所以不属于尾调用，即使语义相同

- 情况二在调用后也有别的操作，所以不属于尾调用，即使在同一行

- 情况三中不管 x 取什么值，最后一步操作都是函数调用，所以属于尾调用

### 尾调用优化

由于尾调用是调用函数的最后一步操作，尾调用返回后，调用函数也就返回了。执行尾调用的时候，调用函数在栈帧中保存的信息都不会再用到了，所以可以用尾调用函数的栈帧覆盖调用函数的栈帧，而不是在调用函数的栈帧上再创建一个新的，这样就可以节省栈空间，即尾调用优化。

考虑如下 C 代码：

```c
int func(void) {
    int m = 1, n = 2;

    return g(m + n);
}
```

`func()` 等同于 `g(3)`，调用之后，`func()` 就结束了，所以执行到 `g(3)` 的时候，完全可以用 `g(3)` 的栈帧覆盖掉 `func()` 的栈帧。

## 尾递归

若一个函数在尾位置调用自身，则称这种情况为**尾递归**，尾递归是递归的一种特殊情况。

### 尾递归优化

当编译器检测到尾递归的时候，它就覆盖当前的栈帧而不是在栈中去创建一个新的。无论调用多少次，只要每次都将栈空间覆盖，则空间占用就是一个常数。

## 比较递归和尾递归

普通递归时，以计算 n! 为例，其函数的 C 代码如下：

```c
int fact(int n) {
    int r;
    if (n <= 1)
        r = 1;
    else
        r = n * fact(n - 1);

    return r;
}
```

当 n = 5 时，其计算过程如下：

```
fact(5)
5 * fact(4)
5 * (4 * fact(3))
5 * (4 * (3 * fact(2)))
5 * (4 * (3 * (2 * fact(1))))
5 * (4 * (3 * (2 * 1)))
5 * (4 * (3 * 2))
5 * (4 * 6)
5 * 24
120
```

可以看到，栈从左到右，增加到一个峰值，然后从右到左缩小。当 n 变得越来越大时，则有栈溢出的可能。

## 使用尾递归

尾递归时，再次以计算 n! 为例，其 C 代码如下：

```c
int fact(int n, int nt) {
    if (n <= 1)
        return nt;
    else
        return fact(n - 1, n * nt);
}

int main(void) {
    printf("%d\n", fact(5, 1));

    return 0;
}
```

当 n = 5 时，其计算过程如下：

```
fact(5, 1)
fact(4, 5*1)       = fact(4, 5)
fact(3, 4*5*1)     = fact(3, 20)
fact(2, 3*4*5*1)   = fact(2, 60)
fact(1, 2*3*4*5*1) = fact(1, 120)
120
```

可以看到，多了一个参数是为了将单次计算的结果存起来，并作为参数传递给下一次调用，每一次调用都离最终结果近了一步，相当于是迭代。

当没有开启优化时，其汇编代码如下：

```assembly
; int fact(int n, int nt)
00A11080  push        ebp
00A11081  mov         ebp,esp
; if (n <= 1)
00A11083  cmp         dword ptr [ebp+8],1
00A11087  jg          00A11090
; return nt;
00A11089  mov         eax,dword ptr [ebp+0Ch]
00A1108C  jmp         00A110A7
00A1108E  jmp         00A110A7
; else
; return fact(n - 1, n * nt);
00A11090  mov         eax,dword ptr [ebp+8]
00A11093  imul        eax,dword ptr [ebp+0Ch]
00A11097  push        eax
00A11098  mov         ecx,dword ptr [ebp+8]
00A1109B  sub         ecx,1
00A1109E  push        ecx
00A1109F  call        00A11080                  ; 调用自身，并创建新的栈帧
00A110A4  add         esp,8
00A110A7  pop         ebp
00A110A8  ret

; int main(void)
00A110B0  push        ebp
00A110B1  mov         ebp,esp
; printf("%d\n", fact(5, 1));
00A110B3  push        1
00A110B5  push        5
00A110B7  call        00A11080                  ; fact(5, 1)
00A110BC  add         esp,8
00A110BF  push        eax
00A110C0  push        0A12100h
00A110C5  call        00A11040
00A110CA  add         esp,8
; return 0;
00A110CD  xor         eax,eax
00A110CF  pop         ebp
00A110D0  ret
```

开启 O2 优化后，其汇编代码如下：

```assembly
; int fact(int n, int nt)
; if (n <= 1)
00FD1040  cmp         ecx,1
00FD1043  jle         00FD104E
; return nt;
; else
; return fact(n - 1, n * nt);
00FD1045  imul        edx,ecx
00FD1048  dec         ecx
00FD1049  cmp         ecx,1
00FD104C  jg          00FD1045      ; 大于则跳转，不创建新的栈帧
00FD104E  mov         eax,edx
00FD1050  ret

; int main(void)
printf("%d\n", fact(5, 1));
00FD1060  mov         edx,5
00FD1065  lea         ecx,[edx-1]
00FD1068  call        00FD1040      ; fact(5, 1)
00FD106D  push        eax
00FD106E  push        0FD2100h
00FD1073  call        00FD1010
00FD1078  add         esp,8
; return 0;
00FD107B  xor         eax,eax
00FD107D  ret
```

可以看到，这里直接使用 jg 指令来跳转，而不是用 call 指令调用函数，这样就不需要创建新的栈帧。
