# 1 移位指令

在算术运算中，编译器会经常使用移位指令来进行优化，特别是当执行与 2<sup>n</sup> 有关的乘法或除法时，能极大的提高效率。

算术运算中的移位主要分为**逻辑移位**和**算术移位**，前者用于**无符号数**的计算，后者用于**有符号数**的计算。而移位又分为**左移**和**右移**，前者主要用于乘法，后者主要用于除法。

## 逻辑移位

逻辑移位指令有 `shl` 和 `shr` 两种，分别代表逻辑左移和逻辑右移，其一般形式为：

```assembly
shl reg/mme,cl/imm8
shr reg/mem,cl/imm8
```

移位次数可以是 `cl` 寄存器或 8 位整数。

逻辑左移每次移位时，最低位用 0 填充，最高位移入 CF 标志位。

![逻辑左移](https://raw.githubusercontent.com/genskyff/image-hosting/main/images/202109111715258.png)

```assembly
mov al,30h  ; al = 00110000b = 48
shl al,2    ; al = 11000000b = 192，CF = 1，相当于 al * 4
```

逻辑右移每次移位时，最高位用 0 填充，最低位移入 CF 标志位。

![逻辑右移](https://raw.githubusercontent.com/genskyff/image-hosting/main/images/202109111742686.png)

```assembly
mov al,A2h  ; al = 10100010b = 162
shr al,3    ; al = 00010100b = 20，CF = 0，相当于 al / 8
```

## 算术移位

算术移位指令有 `sal` 和 `sar` 两种，分别代表算术左移和算术右移，其操作数类型和逻辑移位指令相同，**算术左移与逻辑左移完全相同**。

算术右移每次移位时，最高位用符号位填充，最低位移入 CF 标志位。

![算术右移](https://raw.githubusercontent.com/genskyff/image-hosting/main/images/202109111742180.png)

```assembly
mov al,80h     ; al = 10000000b = -128
sar al,4       ; al = 11111000b = -8，CF = 0，相当于 al / 16
```

## 循环移位

循环移位指令有 `rol` 和 `ror` 两种，分别代表循环左移和循环右移，即从一端移出的位移动到的另一端，因此不会丢弃位，通常用来交换字节的顺序。

循环左移每次移位时，最高位放到 CF 标志位和最低位。

![循环左移](https://raw.githubusercontent.com/genskyff/image-hosting/main/images/202109191842198.png)

```assembly
mov ax,12CDh
rol ax,8        ; ax = CD12，CF = 0
```

循环右移每次移位时，最低位放到 CF 标志位和最高位。

![循环右移](https://raw.githubusercontent.com/genskyff/image-hosting/main/images/202109191842804.png)

```assembly
xor ax,ax
inc ax
ror ax,1    ; 符号位 = 1，CF = 1
```

# 2 加减运算

## 整数

整数的加减运算通常会使用 `add` 和 `sub` 等指令，这两者都是不带进 / 借位的运算，根据结果影响各种标志位。

`adc` / `sbb` 指令为带进 / 借位加减法，其形式与 add / sub 指令相同，但是会在计算最后加上 / 减去 CF 标志位。

```assembly
xor dl,dl
mov al,0FFh
add al,0FFh     ; al = FEh，CF = 1
adc dl,0        ; dl += CF，dl:al = 01FEh
```

在进行编译优化时，会用 lea 指令来替代，因为该指令可以在一个时钟周期内完成，但需要此时有寄存器可用。

如表达式 `c = a + b + 5`，设此时寄存器 eax、ecx，edx 都有效，其优化后的汇编代码可能如下：

```assembly
mov eax,a
mov ecx,b
lea edx,[ecx+edx+5]
```

需要注意的是，lea 指令中，**寄存器与寄存器之间只能用加法**，如下：

```assembly
lea eax,[eax+edx-3]     ; 合法
lea eax,[eax-edx-3]     ; 非法
```

当表达式中出现两个及以上的常量时，编译器会进行**常量折叠**来优化，即在编译期间就已经把常量的值算好，这样在程序执行时就不需要计算，如表达式 `b = a + 10/4 - 6*3`，编译器会直接把其中的表达式 `10/4 - 6*3` 直接计算出来。

## 浮点数

浮点数的四则运算都使用专用的指令来处理。

考虑如下 C 代码：

```c
int main(int argc, char *argv[]) {
    printf("%f\n", (double)argc / 1.5);

    return 0;
}
```

用 x87 指令来处理，其汇编代码如下：

```assembly
; int main(int argc, char *argv[])
00671080  push        ebp
00671081  mov         ebp,esp
00671083  sub         esp,8
; printf("%f\n", (double)argc / 1.5);
00671086  fild        dword ptr [ebp+8]
00671089  fstp        qword ptr [ebp-8]
0067108C  fld         qword ptr [ebp-8]
0067108F  fdiv        qword ptr ds:[00672100h]
00671095  sub         esp,8
00671098  fstp        qword ptr [esp]
0067109B  push        673020h
006710A0  call        00671040
006710A5  add         esp,0Ch
; return 0;
006710A8  xor         eax,eax
006710AA  mov         esp,ebp
006710AC  pop         ebp
006710AD  ret
```

用 SSE 指令来处理，其汇编代码如下：

```assembly
; int main(int argc, char *argv[])
00931080  push        ebp
00931081  mov         ebp,esp
; printf("%f\n", (double)argc / 1.5);
00931083  cvtsi2sd    xmm0,dword ptr [ebp+8]
00931088  divsd       xmm0,mmword ptr ds:[00932100h]
00931090  sub         esp,8
00931093  movsd       mmword ptr [esp],xmm0
00931098  push        933020h
0093109D  call        00931040
009310A2  add         esp,0Ch
; return 0;
009310A5  xor         eax,eax
009310A7  pop         ebp
009310A8  ret
```

可以看到，无论是 x87 还是 SSE，都有一套专门用于处理浮点运算的指令和寄存器。在 x87 下，还需要利用浮点栈寄存器来进行。

# 3 乘法运算

## 整数

乘法运算通常会使用 `mul` 和 `imul` 等指令，乘法指令的速度较慢，在进行编译优化时，会尽量使用其它指令来替代。如当数为 2<sup>n</sup> 时，会使用左移指令 shl 来实现，对于奇数的运算，会尽量转化为移位和加法运算来进行。

考虑如下 C 代码：

```c
int main(int argc, char *argv[]) {
    printf("%d, %d\n", argc * 5, argc * 8);

    return 0;
}
```

其汇编代码如下：

```assembly
; int main(int argc, char *argv[])
00AA1080  push        ebp
00AA1081  mov         ebp,esp
; printf("%d, %d\n", argc * 5, argc * 8);
00AA1083  mov         eax,dword ptr [ebp+8]
00AA1086  shl         eax,3                     ; 移位
00AA1089  push        eax
00AA108A  imul        ecx,dword ptr [ebp+8],5   ; 乘法
00AA108E  push        ecx
00AA108F  push        0AA3020h
00AA1094  call        00AA1040
00AA1099  add         esp,0Ch
; return 0;
00AA109C  xor         eax,eax
00AA109E  pop         ebp
00AA109F  ret
```

mul 是无符号乘法指令，只有一个操作数，执行后如果乘积的高位部分不为零，则 CF 和 OF 标志位会置 1，否则置 0。mul 指令不会发生溢出，其一般形式为：

| 被乘数 |     乘数      | 乘积    |
| -----: | :-----------: | :------ |
|     al |  reg8 / mem8  | ax      |
|     ax | reg16 / mem16 | dx:ax   |
|    eax | reg32 / mem32 | edx:eax |
|    rax | reg64 / mem64 | rdx:rax |

有时候编译或反编译出来的 mul 指令有类似如下的形式：

```assembly
mul eax,ebx     ; 等同于 mul ebx
```

imul 指令是有符号乘法指令，会将乘积进行符号扩展，有 1 ~ 3 个操作数。当只有一个时，其执行逻辑与 mul 相同，也同样不会发生溢出。当操作数大于 1 个时，imul 指令会按照目的操作数的大小来截取乘积，若乘积有效位被丢弃，则 CF 和 OF 标志位会置 1，否则置 0，因此在执行后必须检查这些标志位，利用这个特点可以决定是否忽略乘积的高位部分。

其一般形式如下：

```assembly
imul reg,reg/mem/imm                ; reg = reg * reg/mem/imm
imul reg,reg/mem/imm,reg/mem/imm    ; reg = reg/mem/imm * reg/mem/imm
```

由于有符号和无符号数乘积的低位部分是相同的，因此双操作数和三操作数的 imul 指令也可以用于无符号乘法，但这样将无法利用 CF 和 OF 标志位判断乘积的高位部分是否为零。

编译器通常会使用 lea 指令来对乘法运算进行优化，该指令可以实现寄存器乘以 2、4 或 8 的运算，**但同时只能有一个寄存器含有乘法**，如以下指令：

```assembly
lea eax,[eax*2+ecx+3]       ; 合法
lea eax,[eax*4+ecx*2+5]     ; 非法
lea eax,[eax*8+eax*4+7]     ; 非法
```

如表达式 `a * 2`、`a * 9` 和 `a*11 + 9`，其优化后的汇编代码可能如下：

```assembly
mov eax,a
lea ecx,[eax+eax]       ; a * 2，或 shl eax,1
lea edx,[eax+eax*8]     ; a * 9
lea ecx,[eax+eax*4]
lea edi,[eax+ecx*2+9]   ; a*11 + 9
```

## 浮点数

考虑如下 C 代码：

```c
int main(int argc, char *argv[]) {
    printf("%f\n", (double)argc * 18.2);

    return 0;
}
```

用 x87 指令来处理，其汇编代码如下：

```assembly
; int main(int argc, char *argv[])
00261080  push        ebp
00261081  mov         ebp,esp
00261083  sub         esp,8
; printf("%f\n", (double)argc * 18.2);
00261086  fild        dword ptr [ebp+8]
00261089  fstp        qword ptr [ebp-8]
0026108C  fld         qword ptr [ebp-8]
0026108F  fmul        qword ptr ds:[00262100h]
00261095  sub         esp,8
00261098  fstp        qword ptr [esp]
0026109B  push        263020h
002610A0  call        00261040
002610A5  add         esp,0Ch
; return 0;
002610A8  xor         eax,eax
002610AA  mov         esp,ebp
002610AC  pop         ebp
002610AD  ret
```

用 SSE 指令来处理，其汇编代码如下：

```assembly
; int main(int argc, char *argv[])
00AE1080  push        ebp
00AE1081  mov         ebp,esp
; printf("%f\n", (double)argc * 18.2);
00AE1083  cvtsi2sd    xmm0,dword ptr [ebp+8]
00AE1088  mulsd       xmm0,mmword ptr ds:[00AE2100h]
00AE1090  sub         esp,8
00AE1093  movsd       mmword ptr [esp],xmm0
00AE1098  push        0AE3020h
00AE109D  call        00AE1040
00AE10A2  add         esp,0Ch
; return 0;
00AE10A5  xor         eax,eax
00AE10A7  pop         ebp
00AE10A8  ret
```

可以看到，乘法对于浮点数来说，也是专用的指令，且不像整数那样容易优化。

# 4 除法运算

## 整数

除法运算通常会使用 `div` 和 `idiv` 等指令，除法运算的速度相当慢，比乘法运算还要低一个数量级，编译器在进行优化时，会尽量使用其它指令来替代。

计算机中的除法和数学计算中的除法有所不同，计算机中的是取整除法，因此在移位时可能需要修正。

考虑如下 C 代码：

```c
int main(int argc, char *argv[]) {
    printf("%u, %d\n", (unsigned)argc / 5u, argc / 5);

    return 0;
}
```

其汇编代码如下：

```assembly
; int main(int argc, char *argv[])
00251080  push        ebp
00251081  mov         ebp,esp
; printf("%u, %d\n", (unsigned)argc / 5u, argc / 5);
00251083  mov         eax,dword ptr [ebp+8]
00251086  cdq                               ; 符号扩展
00251087  mov         ecx,5
0025108C  idiv        eax,ecx               ; 有符号乘法
0025108E  push        eax
0025108F  mov         eax,dword ptr [ebp+8]
00251092  xor         edx,edx
00251094  mov         ecx,5
00251099  div         eax,ecx               ; 无符号乘法
0025109B  push        eax
0025109C  push        253020h
002510A1  call        00251040
002510A6  add         esp,0Ch
; return 0;
002510A9  xor         eax,eax
002510AB  pop         ebp
002510AC  ret
```

div 和 idiv 指令的操作数都**只有一个**，前者为无符号除法，后者为有符号除法，两者执行逻辑相同，其一般形式为：

|  被除数 |     除数      | 商   | 余数 |
| ------: | :-----------: | ---- | ---- |
|      ax |  reg8 / mem8  | al   | ah   |
|   dx:ax | reg16 / mem16 | ax   | dx   |
| edx:eax | reg32 / mem32 | eax  | edx  |
| rdx:rax | reg64 / mem64 | rax  | rdx  |

在**执行 div 指令前，如果被除数不需要使用寄存器来存储高位，则必须将代表其高位的寄存器清零**；**执行 idiv 前，必须进行符号扩展**，因此可以根据这个特征来识别除法指令。

```assembly
; 无符号除法
xor dx,dx       ; 清零
mov ax,1000h
div bx

; 有符号除法
mov ax,8000h
cwd             ; 符号扩展
idiv bx
```

有时候编译或反编译出来的 div 或 idiv 指令有类似如下的形式：

```assembly
div eax,ebx     ; 等同于 div ebx
idiv rax,rbx    ; 等同于 idiv rbx
```

若除法运算的商大于操作数的大小，将产生溢出，这会使程序异常并暂停执行，除以 0 也会使程序产生异常，如下：

```assembly
; 除法溢出
mov ax,1000h
mov bl,10h
div bl        ; al 无法容纳 100h

; 除以 0
mov eax,100h
mov bx,0
div bx        ; 除数不能为 0
```

## 整数优化算法

当被除数和除数都是未知数时，编译器将很难做出优化。当被除数或除数为常量时，根据不同情况有不同的优化算法，不同编译器采取的方法也不一定相同。

### 无符号优化算法

无符号数无需判断正负性，因此特征简单。

#### 除数为 2 的幂

当除数为 2<sup>n</sup> 时，使用 shr 指令来替代除法运算，设被除数为 `ux`，其优化公式为：
$$
\frac{ux}{2^n} = ux >> n
$$
如表达式 `ux / 16`，因为 n = 4，则优化后的指令为：

```assembly
shr ux,4
```

#### 除数为非 2 的幂

当除数为非 2<sup>n</sup> 时，使用乘法来替代除法运算，即乘以除数的倒数。有两种优化公式，设被除数为 `ux`，除数为 `d`。

**第一种优化公式**为：
$$
\frac{ux}{d} = \begin{cases}
ux * m >> 32 + k & (x86)\\
ux * m >> 64 + k & (x64)
\end{cases}
$$

其中 $m$ 为编译器生成的 **Magic Number**，利用这个数可以把除法变为倒数相乘，效率可以提升数倍。$k$ 为编译器计算得出的移位次数。

实际的除数的公式为：
$$
d = \frac{2^{32 + k}}{m} \quad (x86)
$$

$$
d = \frac{2^{64 + k}}{m} \quad (x64)
$$

考虑如下 C 代码：

```c
int main(int argc, char *argv[]) {
    printf("%u\n", (unsigned)argc / 13u);

    return 0;
}
```

开启 O2 优化后，其汇编代码如下：

```assembly
; int main(int argc, char *argv[])
00F31040  push        ebp
00F31041  mov         ebp,esp
; printf("%u\n", (unsigned)argc / 13u);
00F31043  mov         eax,4EC4EC4Fh             ; Magic Number
00F31048  mul         eax,dword ptr [ebp+8]     ; edx:eax = ux * m
00F3104B  shr         edx,2                     ; k = 2
                                                ; edx = (ux * m) >> 32 >> 2
                                                ;     = ux * m / 2^34
                                                ;     = ux * 0.0769230769
                                                ;     ≈ ux / 13
00F3104E  push        edx
00F3104F  push        0F32100h
00F31054  call        00F31010
00F31059  add         esp,8
; return 0;
00F3105C  xor         eax,eax
00F3105E  pop         ebp
00F3105F  ret
```

上述程序实际的除数为 `d = 2^34 / 4EC4EC4Fh ≈ 12.9999999977`。

x64 和 x86 执行过程基本相同，此算法下，`ux / d` 可以归纳为：

```assembly
mov ebx,ux
mov eax,Magic Number
mul ebx
shr edx,k
```

**第二种优化公式**为：
$$
\frac{ux}{d} = \{[ux - (ux * m >> 32)] >> k_1\} + (ux * m >> 32) >> k_2 \quad (x86)
$$

$$
\frac{ux}{d} = \{[ux - (ux * m >> 64)] >> k_1\} + (ux * m >> 64) >> k_2 \quad (x64)
$$

其中 $k1$ 和  $k2$ 为编译器计算得出的移位次数。

实际的除数的公式为：
$$
d = \frac{2^{32 + k_1 + k_2}}{(2^{32} + m)} \quad (x86)
$$

$$
d = \frac{2^{64 + k_1 + k_2}}{(2^{64} + m)} \quad (x64)
$$

按照此算法，表达式 `t = ux / 7` 优化后的指令可能为：

```assembly
mov ebx,ux
mov eax,24924925h   ; Magic Number
mul ebx             ; edx:eax = ux * m
sub ebx,edx         ; ebx = ux - ux * m >> 32
shr ebx,1           ; ebx = ux - ux * m >> 32 >> 1，k1 = 1
add edx,ebx         ; edx = ebx + ux * m >> 32
shr edx,2           ; edx = ebx + ux * m >> 32 >> 2，k2 = 2
mov t,edx
```

上述表达式实际的除数为 `d = 2^35 / (2^32 + 24924925h) ≈ 6.9999999993`。

x64 和 x86 执行过程基本相同，在此算法下，`ux / d` 可以归纳为：

```assembly
mov ebx,ux
mov eax,Magic Number
mul ebx
sub ebx,edx
shr edx,k1
add edx,ebx
shr edx,k2
```

### 有符号优化算法

有符号数由于需要判断正负性，因此有很多种情况，特征较为复杂。

#### 除数为 2 的幂

当除数为 2<sup>n</sup> 时，使用 sar 指令来替代除法运算，设被除数为 `ix`，其优化公式为：
$$
\frac{ix}{2^n} = \begin{cases}
ix >> n & ix ≥ 0\\
ix + 2^n -1 >> n & ix < 0
\end{cases}
$$

考虑如下 C 代码：

```c
int main(int argc, char *argv[]) {
    printf("%d, %d\n", argc / 8, -argc / 16);

    return 0;
}
```

开启 O2 优化后，其汇编代码如下：

```assembly
; int main(int argc, char *argv[])
00431040  push        ebp
00431041  mov         ebp,esp
; printf("%d, %d\n", argc / 8, -argc / 16);
00431043  mov         eax,dword ptr [ebp+8]
00431046  cdq                               ; 符号扩展
00431047  and         edx,0Fh               ; edx = argc ≥ 0 ? 0 : 0Fh
0043104A  add         eax,edx               ; eax += argc ≥ 0 ? 0 : 0Fh
0043104C  sar         eax,4                 ; eax >> 4
0043104F  neg         eax                   ; -eax
00431051  push        eax
00431052  mov         eax,dword ptr [ebp+8]
00431055  cdq                               ; 符号扩展
00431056  and         edx,7                 ; edx = argc ≥ 0 ? 0 : 7
00431059  add         eax,edx               ; eax += argc ≥ 0 ? 0 : 7
0043105B  sar         eax,3                 ; eax >> 3
0043105E  push        eax
0043105F  push        432100h
00431064  call        00431010
00431069  add         esp,0Ch
; return 0;
0043106C  xor         eax,eax
0043106E  pop         ebp
0043106F  ret
```

上述代码可以表示为：

```c
// argc / 8
if (argc ≥ 0)
    eax = -(argc >> 4);
else
    eax = -(argc + 0Fh >> 4);

// -argc / 16
if (argc ≥ 0)
    eax = argc >> 3;
else
    eax = argc + 7 >> 3;
```

x64 和 x86 执行过程基本相同，ix / 2<sup>n</sup> 可以归纳为：

```assembly
mov eax,ix
cdq
and edx,2^n-1
add eax,edx
sar eax,n
```

#### 除数为 -2 的幂

当除数为 -2<sup>n</sup> 时，其计算过程和 2<sup>n</sup> 相比基本相同，只在最后多了一条求补指令，其优化公式为：
$$
\frac{ix}{2^n} = \begin{cases}
-(ix >> n) & ix ≥ 0\\
-(ix + 2^n - 1 >> n) & ix < 0
\end{cases}
$$

x64 和 x86 执行过程基本相同，ix / -2<sup>n</sup> 可以归纳为：

```assembly
mov eax,ix
cdq
and edx,2^n-1
add eax,edx
sar eax,n
neg eax         ; 多了一条求补指令
```

#### 除数为非 2 / -2 的幂

当除数为非 2<sup>n</sup> / -2<sup>n</sup> 时，有两种优化公式。设被除数为 `ix`，除数为 `d`。

**第一种优化算法**在除数为正数或负数时都相同，只不过当为正数时，Magic Number 为正数，否则为负数，其优化公式为：
$$
\frac{ix}{d} = \begin{cases}
ix * m >> 32 >> k & ix ≥ 0 \quad (x86)\\
(ix * m >> 32 >> k) + 1 & ix < 0
\end{cases}
$$

$$
\frac{ix}{d} = \begin{cases}
ix * m >> 64 >> k & ix ≥ 0 \quad (x64)\\
(ix * m >> 64 >> k) + 1 & ix < 0
\end{cases}
$$

当除数为正数时，与无符号时的相同，实际的除数的公式为：
$$
d = \frac{2^{32 + k}}{m} \quad (x86)
$$

$$
d = \frac{2^{64 + k}}{m} \quad (x64)
$$

当除数为负数时，实际的除数的公式为：
$$
d = \frac{-2^{32 + k}}{2^{32} - m} \quad (x86)
$$

$$
d = \frac{-2^{64 + k}}{2^{64} - m} \quad (x64)
$$

考虑如下 C 代码：

```c
int main(int argc, char *argv[]) {
    printf("%d, %d\n", argc / 13, -argc / 13);

    return 0;
}
```

开启 O2 优化后，其汇编代码如下：

```assembly
; int main(int argc, char *argv[])
00AD1040  push        ebp
00AD1041  mov         ebp,esp
; printf("%d, %d\n", argc / 13, -argc / 13);
00AD1043  mov         eax,0B13B13B1h        ; 负数 Magic Number
00AD1048  imul        dword ptr [ebp+8]     ; edx:eax = ix * m
00AD104B  sar         edx,2                 ; k = 2
                                            ; edx = ix * m >> 32 >> 2
00AD104E  mov         eax,edx
00AD1050  shr         eax,1Fh               ; 只留下符号位
00AD1053  add         eax,edx               ; eax += eax ≥ 0 ? 0 : 1
00AD1055  push        eax
00AD1056  mov         eax,4EC4EC4Fh         ; 正数 Magic Number
00AD105B  imul        dword ptr [ebp+8]     ; edx:eax = ix * m
00AD105E  sar         edx,2                 ; k = 2
                                            ; edx = ix * m >> 32 >> 2
00AD1061  mov         eax,edx
00AD1063  shr         eax,1Fh               ; 只留下符号位
00AD1066  add         eax,edx               ; eax += eax ≥ 0 ? 0 : 1
00AD1068  push        eax
00AD1069  push        0AD2100h
00AD106E  call        00AD1010
00AD1073  add         esp,0Ch
; return 0;
00AD1076  xor         eax,eax
00AD1078  pop         ebp
00AD1079  ret
```

第 3 个参数 `-argc / 13` 可以转化成 `argc / -13`，即除数为负数，因此生成的 Magic Number 也是负数，其实际的除数为 `d = -2^34 / (2^32 - 0B13B13B1h) ≈ -12.9999999977`。第 2 个参数的除数为正数，因此 Magic Number 也是正数，其实际的除数为 `d = 2^34 / 4EC4EC4Fh ≈ 12.9999999977`。

上述代码可以表示为：

```c
if (argc ≥ 0)
    eax = argc * m >> 34;
else
    eax = (argc * m >> 34) + 1;
```

x64 和 x86 执行过程基本相同，此算法下，`ix / d` 可以归纳为：

```assembly
mov ebx,ix
mov eax,Magic Number
imul ebx
sar edx,k
mov eax,edx
shr eax,1Fh
add eax,edx
```

**第二种优化算法**要区分除数的正负性，和第一种相反，当除数为正数时，Magic Number 为负数，其优化公式为：
$$
\frac{ix}{d} = \begin{cases}
(ix * m >> 32) + ix >> k & ix ≥ 0 \quad (x86)\\
[(ix * m >> 32) + ix >> k] + 1 & ix < 0
\end{cases}
$$

$$
\frac{ix}{d} = \begin{cases}
(ix * m >> 64) + ix >> k & ix ≥ 0 \quad (x64)\\
[(ix * m >> 64) + ix >> k] + 1 & ix < 0
\end{cases}
$$

当除数为负数时，Magic Number 为正数，其优化公式为：
$$
\frac{ix}{d} = \begin{cases}
(ix * m >> 32) - ix >> k & ix ≥ 0 \quad (x86)\\
[(ix * m >> 32) - ix >> k] + 1 & ix < 0
\end{cases}
$$

$$
\frac{ix}{d} = \begin{cases}
(ix * m >> 64) - ix >> k & ix ≥ 0 \quad (x64)\\
[(ix * m >> 64) - ix >> k] + 1 & ix < 0
\end{cases}
$$

按照此算法，则表达式 `t = ix / 7` 和 `t = ix / -7` 优化后的指令可能为：

```assembly
; t = ix / 7
mov ebx,ix
mov eax,92492493h   ; 负数 Magic Number
imul ebx            ; edx:eax = ix * m
add edx,ebx         ; edx = (ix * m >> 32) + ix
sar edx,2           ; edx = (ix * m >> 32) + ix >> 2，k = 2
mov eax,edx
shr eax,1Fh         ; eax = edx > 0 ? 0 : 1
add edx,eax         ; edx += ((ix * m >> 32) + ix >> 2) + 1
mov t,edx

; t = ix / -7
mov ebx,ix
mov eax,6DB6DB6Dh   ; 正数 Magic Number
imul ebx            ; edx:eax = ix * m
sub edx,ebx         ; edx = (ix * m >> 32) - ix
sar edx,2           ; edx = (ix * m >> 32) - ix >> 2，k = 2
mov eax,edx
shr eax,1Fh         ; eax = edx > 0 ? 0 : 1
add edx,eax         ; edx += ((ix * m >> 32) - ix >> 2) + 1
mov t,edx
```

可以看到，两者只有在 add 和 sub 指令上有区别，其前者实际的除数为 `d = 2^34 / 92492493h ≈ 6.9999999979`，后者实际的除数为 `d = -2^34 / (2^32 - 6DB6DB6Dh) ≈ -6.9999999979`。

x64 和 x86 执行过程基本相同，此算法下，`ix / d` 可以归纳为：

```assembly
mov ebx,ix
mov eax,m
imul ebx
add edx,ebx     ; 除数为负数时为 sub edx,ebx
sar edx,k
mov eax,edx
shr eax,1Fh
add edx,eax
```

## 浮点数

考虑如下 C 代码：

```c
int main(int argc, char *argv[]) {
    double d = (double)argc;
    printf("%f, %f\n", d / 2.3, d / 8.0);

    return 0;
}
```

用 x87 指令来处理，其汇编代码如下：

```assembly
; int main(int argc, char *argv[])
006A1080  push        ebp
006A1081  mov         ebp,esp
006A1083  sub         esp,8
; double d = (double)argc;
006A1086  fild        dword ptr [ebp+8]
006A1089  fstp        qword ptr [ebp-8]
; printf("%f, %f\n", d / 2.3, d / 8.0);
006A108C  fld         qword ptr [ebp-8]
006A108F  fdiv        qword ptr ds:[006A2108h]
006A1095  sub         esp,8
006A1098  fstp        qword ptr [esp]
006A109B  fld         qword ptr [ebp-8]
006A109E  fdiv        qword ptr ds:[006A2100h]
006A10A4  sub         esp,8
006A10A7  fstp        qword ptr [esp]
006A10AA  push        6A3020h
006A10AF  call        006A1040
006A10B4  add         esp,14h
; return 0;
006A10B7  xor         eax,eax
006A10B9  mov         esp,ebp
006A10BB  pop         ebp
006A10BC  ret
```

用 SSE 指令来处理，其汇编代码如下：

```assembly
; int main(int argc, char *argv[])
00D41080  push        ebp
00D41081  mov         ebp,esp
00D41083  sub         esp,8
; double d = (double)argc;
00D41086  cvtsi2sd    xmm0,dword ptr [ebp+8]
00D4108B  movsd       mmword ptr [ebp-8],xmm0
; printf("%f, %f\n", d / 2.3, d / 8.0);
00D41090  movsd       xmm0,mmword ptr [ebp-8]
00D41095  divsd       xmm0,mmword ptr ds:[00D42108h]
00D4109D  sub         esp,8
00D410A0  movsd       mmword ptr [esp],xmm0
00D410A5  movsd       xmm0,mmword ptr [ebp-8]
00D410AA  divsd       xmm0,mmword ptr ds:[00D42100h]
00D410B2  sub         esp,8
00D410B5  movsd       mmword ptr [esp],xmm0
00D410BA  push        0D43020h
00D410BF  call        00D41040
00D410C4  add         esp,14h
; return 0;
00D410C7  xor         eax,eax
00D410C9  mov         esp,ebp
00D410CB  pop         ebp
00D410CC  ret
```

可以看到，对于除法，即使除数是 2 的幂，和普通的除法也相同。

对浮点数的优化，通常是使用位数更长的浮点寄存器来进行并行运算。对于上述代码，使用 SSE 指令，开启 O2 优化后，其汇编代码如下：

```assembly
; int main(int argc, char *argv[])
001A1050  push        ebp
001A1051  mov         ebp,esp
001A1053  and         esp,0FFFFFFC0h
001A1056  movd        xmm1,dword ptr [ebp+8]
; double d = (double)argc;
; printf("%f, %f\n", d / 2.3, d / 8.0);
001A105B  sub         esp,10h
001A105E  cvtdq2pd    xmm1,xmm1
001A1062  movaps      xmm0,xmm1
001A1065  divsd       xmm1,mmword ptr ds:[001A2110h]
001A106D  mulsd       xmm0,mmword ptr ds:[001A2108h]
001A1075  movsd       mmword ptr [esp+8],xmm0
001A107B  movsd       mmword ptr [esp],xmm1
001A1080  push        1A2100h
001A1085  call        001A1010
001A108A  add         esp,14h
; return 0;
001A108D  xor         eax,eax
001A108F  mov         esp,ebp
001A1091  pop         ebp
001A1092  ret
```

# 5 求余运算

在不进行优化的情况下，由于除法运算会把余数放到高位寄存器中，因此求余运算可以用除法运算实现，但除法指令周期较长，因此会优化成等价的位运算或除法运算，再将除法运算进行优化。

对于求余运算，余数的正负性只与被除数有关，因此不需要区分除数是否为负数的情况。

求余运算只能用于整数，因此不需要讨论浮点数的情况。

## 除数为 2 的幂

对于 2<sup>n</sup> 的求余来说，只需取得被除数二进制位的最后 n 位即可，对于负数在之前补 1。

有两种优化算法，设被除数为 `x`，当 x ≥ 0 时，这两种优化公式相同，如下：
$$
x\ \%\ 2^n = x\ \&\ (2^n - 1)
$$
当 x < 0 时，这两种优化公式为：
$$
x\ \%\ 2^n = \{[x\ \&\ (2^n - 1)] - 1\ | \sim(2^n - 1)\} + 1
$$

$$
x \%\ 2^n = [x + (2^n - 1)\ \&\ (2^n - 1)] - (2^n - 1)
$$

考虑如下 C 代码：

```c
int main(int argc, char *argv[]) {
    printf("%d\n", argc % 8);

    return 0;
}
```

开启 O2 优化后，其汇编代码如下：

```assembly
; int main(int argc, char *argv[])
005A1080  push        ebp
005A1081  mov         ebp,esp
; printf("%d\n", argc % 8);
005A1083  mov         eax,dword ptr [ebp+8]
005A1086  and         eax,80000007h             ; eax = argc & 2^3 - 1
005A108B  jns         005A1092                  ; 正数求完余直接跳转
005A108D  dec         eax
005A108E  or          eax,0FFFFFFF8h            ; eax |= ~(2^3 - 1)
005A1091  inc         eax
005A1092  push        eax
005A1093  push        5A3020h
005A1098  call        005A1040
005A109D  add         esp,8
; return 0;
005A10A0  xor         eax,eax
005A10A2  pop         ebp
005A10A3  ret
```

将 argc 和 `80000007h` 做位与运算是为了保留符号位，因为正数求完余就直接跳转不需要做接下来的一系列运算。

## 除数为非 2 的幂

对于非 2<sup>n</sup> 的求余来说，设被除数为 `x`，除数为 `d`，其优化公式为：
$$
x\ \%\ d = x - \frac{x}{d} * d
$$
考虑如下 C 代码：

```c
int main(int argc, char *argv[]) {
    printf("%d\n", argc % 13);

    return 0;
}
```

开启 O2 优化后，其汇编代码如下：

```assembly
; int main(int argc, char *argv[])
00011040  push        ebp
00011041  mov         ebp,esp
; printf("%d\n", argc % 13);
00011043  mov         ecx,dword ptr [ebp+8]
00011046  mov         eax,4EC4EC4Fh         ; Magic Number
0001104B  imul        ecx
0001104D  sar         edx,2
00011050  mov         eax,edx
00011052  shr         eax,1Fh
00011055  add         eax,edx               ; x / 13
00011057  imul        eax,eax,0Dh           ; x * 13
0001105A  sub         ecx,eax               ; x - x / 13 * 13
0001105C  push        ecx
0001105D  push        12100h
00011062  call        00011010
00011067  add         esp,8
; return 0;
0001106A  xor         eax,eax
0001106C  pop         ebp
0001106D  ret
```

可以看到，先进行优化除法的 `x / 13` 计算，再进行乘法运算，最后再相减。
